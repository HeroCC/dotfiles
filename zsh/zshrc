# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

if [[ -r "${ZINIT_MODULE_PATH:=${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/module/Src}/zdharma_continuum/zinit.so" ]]; then
  # Automatically compile `source` usages -- use after `zinit module build`
  module_path+=( "$ZINIT_MODULE_PATH" )
  zmodload zdharma_continuum/zinit
fi

[[ -z "$SSH_CONNECTION" && ! "$TERMINAL_EMULATOR" =~ "JetBrains-JediTerm" && ! "$TERM_PROGRAM" =~ "vscode" ]] && ZSH_TMUX_AUTOSTART="true"
if [[ -z "$TMUX" && "$ZSH_TMUX_AUTOSTART" -eq "true" ]] && (( $+commands[tmux] )); then
  # Only reattach to disconnected tmux sessions, otherwise open a new one
  ZSH_TMUX_DEFAULT_SESSION_NAME="$(tmux list-sessions -F '#{?session_attached,,#{session_name}}' 2>/dev/null | grep -v '^$' | head -1)"
  [ -z "$ZSH_TMUX_DEFAULT_SESSION_NAME" ] && ZSH_TMUX_AUTOCONNECT="false"
fi

# Add Plugins
declare -A ZINIT && ZINIT[NO_ALIASES]="true"
source "$HOME/.zsh/zinit/zinit.zsh"
zinit snippet OMZ::plugins/brew
zinit svn is-snippet for OMZP::tmux
zinit light NICHOLAS85/z-a-eval

zinit lucid svn is-snippet atinit'zstyle :omz:plugins:iterm2 shell-integration yes' for OMZP::iterm2
zinit depth"1" atload'zinit is-snippet for $HOME/.zsh/themes/p10k.zsh' for romkatv/powerlevel10k

while read line; do
  zinit snippet OMZ::lib/${line}
done <<EOLIBS
git.zsh
key-bindings.zsh
grep.zsh
completion.zsh
directories.zsh
EOLIBS

while read line; do
  zinit ice wait lucid
  zinit snippet OMZ::plugins/${line}
done <<EOPLUGINS
magic-enter
command-not-found
colored-man-pages
extract
git
EOPLUGINS

if (( $+commands[atuin] )); then
  # Purposefully not `wait`ing async -- my shell history is important
  zinit has'atuin' for atuinsh/atuin
elif (( $+commands[fzf] )); then
  zinit lucid wait is-snippet for OMZ::plugins/fzf
fi

zinit lucid wait has'pyenv-virtualenv-init' eval'pyenv virtualenv-init -' id-as'pyenv-venv' nocompile'!' for @zdharma-continuum/null
zinit lucid wait for agkozak/zsh-z
zinit lucid wait for zpm-zsh/ls
zinit lucid wait for olets/zsh-abbr
zinit lucid wait pack for ls_colors
zinit lucid wait has'fzf' for Aloxaf/fzf-tab
zinit lucid wait has'thefuck' for OMZ::plugins/thefuck
zinit lucid wait atinit'YSU_MESSAGE_POSITION="after" YSU_IGNORED_ALIASES+=("g")' for MichaelAquilina/zsh-you-should-use
zinit lucid wait atinit"zicompinit; zicdreplay" for zdharma-continuum/fast-syntax-highlighting
zinit lucid wait for Myzel394/zsh-copilot
zinit lucid wait atinit"ZSH_AUTOSUGGEST_MANUAL_REBIND=1" atload'!_zsh_autosuggest_start' for zsh-users/zsh-autosuggestions

# Source all the configs
while read line; do
  zinit lucid is-snippet for $line
done <<-EOPLUGINS
$HOME/.zsh/history.zsh
$HOME/.zsh/functions.zsh
$HOME/.zsh/aliases.zsh
$HOME/.zsh/exports.zsh
EOPLUGINS
zinit lucid if'[ -e "$HOME/.zsh/local.zsh" ]' is-snippet for ~/.zsh/local.zsh
