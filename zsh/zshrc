# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

if [[ -r "${ZINIT_MODULE_PATH:=${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/module/Src}/zdharma_continuum/zinit.so" ]]; then
  # Automatically compile `source` usages -- use after `zinit module build`
  module_path+=( "$ZINIT_MODULE_PATH" )
  zmodload zdharma_continuum/zinit
fi

[[ -z "$SSH_CONNECTION" && ! "$TERMINAL_EMULATOR" =~ "JetBrains-JediTerm" ]] && ZSH_TMUX_AUTOSTART="true"
if [[ -z "$TMUX" && "$ZSH_TMUX_AUTOSTART" -eq "true" ]] && (( $+commands[tmux] )); then
  # Only reattach to disconnected tmux sessions, otherwise open a new one
  ZSH_TMUX_DEFAULT_SESSION_NAME="$(tmux list-sessions -F '#{?session_attached,,#{session_name}}' 2>/dev/null | grep -v '^$' | head -1)"
  [ -z "$ZSH_TMUX_DEFAULT_SESSION_NAME" ] && ZSH_TMUX_AUTOCONNECT="false"
fi

# Add Plugins
source "$HOME/.zsh/zinit/zinit.zsh"

while read line; do 
  zinit snippet OMZ::lib/${line} 
done <<EOLIBS
git.zsh
key-bindings.zsh
grep.zsh
EOLIBS

while read line; do
  zinit snippet OMZ::plugins/${line} 
done <<EOPLUGINS
brew
magic-enter
EOPLUGINS

zinit snippet OMZ::plugins/tmux

while read line; do
  zinit ice wait lucid
  zinit snippet OMZ::plugins/${line} 
done <<EOPLUGINS
command-not-found
colored-man-pages
extract
git
EOPLUGINS

zinit ice lucid wait atclone"dircolors -b LS_COLORS > clrs.zsh" \
    atpull'%atclone' pick"clrs.zsh" nocompile'!' \
    atload'zstyle ":completion:*" list-colors "${(s.:.)LS_COLORS}"'
zinit light trapd00r/LS_COLORS

zinit blockf for zsh-users/zsh-completions
zinit depth"1" atload"source $HOME/.zsh/themes/p10k.zsh" for romkatv/powerlevel10k
zinit lucid wait for Aloxaf/fzf-tab agkozak/zsh-z
zinit lucid wait atload'_zsh_autosuggest_start' for zsh-users/zsh-autosuggestions
zinit lucid wait atinit"zicompinit; zicdreplay" for zdharma-continuum/fast-syntax-highlighting

# Source all the configs
source ~/.zsh/history.zsh
source ~/.zsh/functions.zsh
source ~/.zsh/aliases.zsh
source ~/.zsh/exports.zsh
[ -e "$HOME/.zsh/local.zsh" ] && source ~/.zsh/local.zsh
